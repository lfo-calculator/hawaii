## 607-4 District court costs

> Begin metadata

```catala
declaration enumeration FeeCategory:
  -- Paragraph7
  -- Paragraph8
  -- Paragraph9

declaration scope S_607_4:
  # Input:
  context violation content Violation

  # Internal:
  context category content FeeCategory

  # Output:
  context penalties content collection Penalty
```

> End metadata

## [§607-4 District court costs.]

(a) The fees prescribed by subsection (b) shall be paid to the clerk of the
district court as costs of court by the person instituting the action or
proceeding, or offering the paper for filing, or causing the document to be
issued or the services to be performed in the district court; provided that
nothing in subsection (b) shall apply to cases of adults charged with commission
of a crime, or minors referred to the district court by the family court;
provided further that for the purposes of subsection (b), “judgment” includes an
order from which an appeal lies; and provided further that the fees prescribed
by subsection (b)(10) shall be deposited by the clerk of the district court into
the judiciary computer system special fund pursuant to section 601-3.7. One-half
of the fees collected pursuant to paragraphs (7), (8), and (9) of subsection (b)
also shall be deposited into the fund.


(b) The fees referred to in subsection (a) are:

(1) Except for petitions for temporary restraining order under section 604-10.5,
the fee for which shall be the same as that provided in section 607-5(b)(19),
for the institution of each action or proceeding, to include all charges except
as provided by paragraphs (2) to (6)………… $100

(2) Intervention; answer containing one or more cross-claims or counterclaims;
third-party complaint, for each such matter…………….. $10

(3) Demand for jury trial.. Fee prescribed by section 607-5

(4) Filing of notice of appeal, to be paid in addition to the deposit of
appellate court costs. $100

(5) Making of a copy; comparing of copy with original Fees prescribed by section
92-21

(6) Posting notice; service fees; garnishee fees; mileage charges; or other
services actually performed Amounts necessary to cover actual costs or
disbursements

(7) Administrative costs associated with the processing of traffic citations
that involve stopping (when prohibited), standing, or parking…. $10 for each
violation in addition to any fine imposed by the court, and whether or not such
fine is suspended

```catala
scope S_607_4:

  definition category under condition
    false
  consequence equals Paragraph7

  definition penalties under condition category = Paragraph7
  consequence equals [
    One content (Fee content (Fee {
      -- min_fee: $10
      -- max_fee: $10
      -- fund: Clerk
    }))]
```

(8) Administrative costs associated with the processing of traffic citations
which do not involve stopping, standing, or parking… $40 for each violation in
addition to any fine imposed by the court, and whether or not such fine is
suspended

```catala
scope S_607_4:

  definition category under condition
    false
  consequence equals Paragraph8

  definition penalties under condition category = Paragraph8
  consequence equals [
    One content (Fee content (Fee {
      -- min_fee: $40
      -- max_fee: $40
      -- fund: Clerk
    }))]
```

(9) Administrative costs associated with the processing of traffic citations
issued for violations of a statute or ordinance relating to vehicles or their
drivers, or owners, except those as provided by paragraphs (7) and (8). $30 for
each violation in addition to any fine imposed by the court, and whether or not
such fine is suspended

```catala
scope S_607_4:

  definition category under condition
    violation = Section286_12 or
    violation = Section286_21 or
    violation = Section286_22 or
    violation = Section286_83 or
    violation = Section286_85 or
    violation = Section286_102 or
    violation = Section286_102_3 or
    violation = Section286_102_6 or
    violation = Section286_116 or
    violation = Section286_116_5 or
    violation = Section286_122 or
    violation = Section286_130 or
    violation = Section286_131 or
    violation = Section286_132 or
    violation = Section286_133 or
    violation = Section286_134 or
    violation = Section286_135 or
    violation = Section291_3_2 or
    violation = Section291_3_3 or
    violation = Section291_4_6 or
    violation = Section291_8 or
    violation = Section291_9 or
    violation = Section291_11 or
    violation = Section291_11_5 or
    violation = Section291_11_6 or
    violation = Section291_12 or
    violation = Section291_13 or
    violation = Section291_14 or
    false
  consequence equals Paragraph9

  # NB: this intentionally errors out if we haven't yet classified the `offense`
  # in this section
  definition penalties under condition category = Paragraph9
  consequence equals [
    One content (Fee content (Fee {
      -- min_fee: $30
      -- max_fee: $30
      -- fund: Clerk
    }))]
```

(10) Administrative costs associated with the processing of all civil filings
except those brought by the State or any of the various counties and political
subdivisions of the State, those commenced by a petition for temporary
restraining order under section 604-10.5, and those commenced and conducted in
the small claims division of the district court…….. $20.

(c) The court, in taxing costs, may assess not only the costs of court, but also
all reasonable disbursements as provided by section 607-9.

(d) [Repeal and reenactment of subsection on June 30, 2020. L 2015, c 101, §4.]
Fees of sheriff, deputy sheriff, police officer, or independent civil process
server from the department of public safety’s list under section 353C-10 shall
be as provided under section 607-8(a).

(e) Anything in this section or any other law to the contrary notwithstanding,
when any process or subpoena is served by a subordinate of the sheriff or chief
of police, it shall be illegal for the sheriff or chief of police, (1) if and so
long as the sheriff or chief of police is being paid a salary by the State or
the county to receive or collect from such subordinate any portion of the fees,
mileage, or other expenses collected by such subordinate, or (2) if and so long
as the sheriff or chief of police is not being paid any such salary, to collect
or receive from such subordinate more than ten per cent of the fees accruing
from such service, or any portion of the mileage or other expenses collected by
such subordinate. Where a subpoena is served in behalf of the State or any
county by a nonsalaried subordinate of the sheriff or chief of police, the
regular fee for such service shall be payable to such subordinate. Nothing
herein contained shall be deemed to prohibit the police commission of any county
from requiring all such fees, mileage, and expenses to be paid into a police
benefit fund. [CC 1859, §1278; am imp L 1903, c 63, §1; am L 1919, c 58, §1; am
L 1923, c 229, §1; RL 1925, §2541; am L 1933, c 47, §1; RL 1935, §3790; am L
1935, c 177, §1; RL 1945, §9744; am L 1945, c 55, §1; am L 1949, c 387, §1; RL
1955, §219-4; am L 1957, c 235, §1; am L 1963, c 85, §3; HRS §607-4; am L 1968,
c 61, §2; am L 1969, c 23, §1; am L 1970, c 188, §39; am L 1972, c 88, §5(d),
(e), (f); am L 1973, c 55, §1; am L 1974, c 145, §6 and c 149, §1; am L 1975, c
112, §1; am L 1978, c 127, §1; am L 1979, c 111, §24; am L 1980, c 96, §1; am L
1984, c 52, §1; gen ch 1985; am L 1989, c 211, §10; am L 1990, c 10, §1, c 58,
§1, and c 281, §11; am L 1991, c 140, §2; am L 1998, c 128, §1; am L 1999, c 92,
§1; am L 2001, c 214, §2; am L 2003, c 216, §2; am L 2004, c 202, §61 and c 231,
§2; am L 2005, c 22, §42; am L 2006, c 94, §1; am L 2010, c 109, §1; am L 2012,
c 142, §3; am L 2013, c 116, §§5, 25(4); am L 2015, c 125, §1]

Note

The L 2015, c 125 amendment is exempt from the repeal and reenactment condition
of L 2013, c 116, §25. L 2015, c 125, §5.

Cross References

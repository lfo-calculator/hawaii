## 286-136 Penalty

> Begin metadata

```catala
declaration scope S_286_136:

  # Inputs:

  context violation content Violation
  context defendant content Defendant
  context two_violations_past_five_years content boolean

  # Internal:

  context max_fine content money
  context min_fine content money
  context max_days content duration

  context paragraph_b_applies condition
  context paragraph_c_applies condition

  # Outcomes:

  context penalties content collection Penalty
```

> End metadata

## [286-136 Penalty]

§(a) Except as provided in subsection (b), any person who
violates section 286-102, 286-122, 286-130, 286-131, 286-132, 286-133, or
286-134 shall be fined not more than $1,000 or imprisoned not more than thirty
days, or both. Any person who violates any other section in this part shall be
fined not more than $1,000.

```catala
scope S_286_136:

  definition min_fine equals $0
  definition max_fine equals $1000
  definition max_days equals 0 day

  definition penalties equals [
    One content (Imprisonment content (Imprisonment {
      -- max_days: max_days
      -- min_days: 0 day
    }));
    One content (Fine content (Fine {
      -- min_fine: min_fine
      -- max_fine: max_fine
    }))
  ]

  label max_days_paragraph_a
  exception definition max_days under condition
    violation with pattern Section286_102 or
    violation with pattern Section286_122 or
    violation with pattern Section286_130 or
    violation with pattern Section286_131 or
    violation with pattern Section286_132 or
    violation with pattern Section286_133 or
    violation with pattern Section286_134
  consequence equals 30 day
```

(b) Any person who is convicted of violating section 286-102, 286-122, 286-130,
286-131, 286-132, 286-133, or 286-134 shall be subject to a minimum fine of $500
and a maximum fine of $1,000, or imprisoned not more than one year, or both, if
the person has two or more prior convictions for the same offense in the
preceding five-year period.

```catala
scope S_286_136:

  # Under subsection (b) (1996), it is the date the defendant committed the current
  # offense for which he or she is being prosecuted that is used to determine
  # whether the defendant has two or more prior convictions for the same offense in
  # the preceding five-year period. 118 H. 259 (App.), 188 P.3d 773 (2008).

  rule paragraph_b_applies under condition
    (violation with pattern Section286_102 or
      violation with pattern Section286_122 or
      violation with pattern Section286_130 or
      violation with pattern Section286_131 or
      violation with pattern Section286_132 or
      violation with pattern Section286_133 or
      violation with pattern Section286_134
    ) and
    two_violations_past_five_years and
    not paragraph_c_applies
  consequence fulfilled

  exception definition min_fine under condition
    paragraph_b_applies
  consequence equals $500

  exception max_days_paragraph_a definition max_days under condition
    paragraph_b_applies
  consequence equals 1 year
```

(c) Notwithstanding subsections (a) and (b), a minor under the age of eighteen
under the jurisdiction of the family court who is subject to this section shall
either lose the right to drive a motor vehicle until the age of eighteen or be
subject to a fine of $500. [L 1967, c 214, pt of §2; HRS §286-136; am L 1993, c
214, §7; am L 1996, c 169, §3; am L 2003, c 69, §5]

```catala
scope S_286_136:

  rule paragraph_c_applies under condition
    # TODO: check what happens if the offense is committed on the day of the
    # birthday
    defendant.age < 18 # and minor and under the jurisdiction etc.
  consequence fulfilled

  exception definition penalties under condition paragraph_c_applies
  consequence equals [ Either content ([
    LoseRightToDriveUntil18;
    Fine content (Fine {
      -- min_fine: $ 500
      -- max_fine: $ 500
    })
  ])]
```

